plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.9.21'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

description = "Plugin that sets up android and java libraries mavenCentral publishing configurations"

repositories {
    mavenCentral()
    gradlePluginPortal()
    google()
}

configurations {
    fatDependency
    compileOnly.extendsFrom(fatDependency)
}

def javaVersion = JavaVersion.VERSION_1_8
def minKotlinVersion = "1.6"

tasks.withType(KotlinCompile).all {
    kotlinOptions {
        jvmTarget = javaVersion.toString()
        apiVersion = minKotlinVersion
        languageVersion = minKotlinVersion
    }
}

dependencies {
    api 'org.jetbrains.dokka:dokka-gradle-plugin:1.8.10'
    implementation 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
    implementation 'com.gradle.publish:plugin-publish-plugin:1.0.0'
    compileOnly "com.android.tools.build:gradle:7.4.0"
    fatDependency "io.github.gradle-nexus:publish-plugin:1.1.0"
}

jar {
    from configurations.fatDependency.collect { zipTree(it) }
}

gradlePlugin {
    plugins {
        publisher {
            id = 'artifact-publisher'
            implementationClass = 'com.likethesalad.tools.artifact.publisher.ArtifactPublisherPlugin'
        }
    }
    automatedPublishing = false
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

ext {
    publicName = "Artifact Publisher"
    url = "https://github.com/LikeTheSalad/artifact-publisher"
    vcsUrl = "https://github.com/LikeTheSalad/artifact-publisher.git"
    issueTrackerUrl = "https://github.com/LikeTheSalad/artifact-publisher/issues"
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            version = project.version

            artifact sourcesJar
            artifact javadocJar
            from components.java

            pom {
                name = project.publicName
                description = project.description
                url = project.url
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'LikeTheSalad'
                        name = 'Cesar Munoz'
                        email = 'likethesalad@gmail.com'
                    }
                }
                scm {
                    url = project.url
                    connection = project.vcsUrl
                }
                issueManagement {
                    url = project.issueTrackerUrl
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}