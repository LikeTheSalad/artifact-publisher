plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.9.21'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

description = "Internal plugin for publishing LikeTheSalad Java and Android libs"
ext {
    publicName = "Artifact Publisher"
    url = "https://github.com/LikeTheSalad/artifact-publisher"
    vcsUrl = "https://github.com/LikeTheSalad/artifact-publisher.git"
    issueTrackerUrl = "https://github.com/LikeTheSalad/artifact-publisher/issues"
}

repositories {
    mavenCentral()
    gradlePluginPortal()
    google()
}

def javaVersion = JavaVersion.VERSION_11
def minKotlinVersion = "1.6"

tasks.withType(KotlinCompile).all {
    kotlinOptions {
        jvmTarget = javaVersion.toString()
        apiVersion = minKotlinVersion
        languageVersion = minKotlinVersion
    }
}

dependencies {
    api 'org.jetbrains.dokka:dokka-gradle-plugin:1.8.10'
    implementation 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
    implementation 'com.gradle.publish:plugin-publish-plugin:1.2.1'
    implementation "io.github.gradle-nexus:publish-plugin:1.1.0"
}

gradlePlugin {
    website.set(project.url)
    vcsUrl.set(project.vcsUrl)
    plugins {
        publisher {
            id = 'com.likethesalad.artifact-publisher'
            displayName = project.publicName
            description = project.description
            tags.set(['publisher', "likethesalad"])
            implementationClass = 'com.likethesalad.tools.artifact.publisher.ArtifactPublisherPlugin'
        }
    }
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            pom {
                name = project.publicName
                description = project.description
                url = project.url
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'LikeTheSalad'
                        name = 'Cesar Munoz'
                        email = 'likethesalad@gmail.com'
                    }
                }
                scm {
                    url = project.url
                    connection = project.vcsUrl
                }
                issueManagement {
                    url = project.issueTrackerUrl
                }
            }
        }
    }
}